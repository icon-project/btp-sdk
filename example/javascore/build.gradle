import foundation.icon.gradle.plugins.btp.task.GenerateContractSpecTask
import foundation.icon.gradle.plugins.javaee.task.OptimizedJar

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'foundation.icon:gradle-btp-plugin:0.1.0-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'foundation.icon.btp'

version = '0.1.0'

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

ext {
    javaeeVersion = getProperty("javaee.version")
    jacksonVersion = getProperty("jackson.version")
    scoreClientVersion = "0.10.3"
}

dependencies {
    implementation "foundation.icon:javaee-api:$javaeeVersion"
    implementation("foundation.icon:javaee-scorex:0.5.3")

    implementation 'foundation.icon:btp2-xcall:0.6.2'

    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    compileOnly "foundation.icon:javaee-score-client:$scoreClientVersion"
    annotationProcessor "foundation.icon:javaee-score-client:$scoreClientVersion"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    testAnnotationProcessor "foundation.icon:javaee-score-client:$scoreClientVersion"
    testImplementation "foundation.icon:javaee-score-client:$scoreClientVersion"

    // for ReflectionToStringBuilder
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'

    // for FieldVisibility
    testImplementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
}

jar {
    manifest {
        attributes(
                'Main-Class' : 'foundation.icon.btp.example.HelloWorld'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } { exclude "score/*" }
    archiveName("hello-world-" + archiveVersion.get() + "." + archiveExtension.get());
}

task jarDAppSample(type: Jar) {
    manifest {
        attributes(
            'Main-Class' : 'foundation.icon.btp.example.DAppSample'
        )
    }
    from { sourceSets.main.output }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } { exclude "score/*" }
    archiveName("dapp-sample-" + archiveVersion.get() + "." + archiveExtension.get());
}

optimizedJar {
    mainClassName = 'foundation.icon.btp.example.HelloWorld'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } { exclude "score/*" }
    archiveName("hello-world-" + archiveVersion.get() + "." + archiveExtension.get());
    enableDebug = true
}

task optimizedJarDAppSample(type: OptimizedJar) {
    mainClassName = 'foundation.icon.btp.example.DAppSample'
    from { sourceSets.main.output }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } { exclude "score/*" }
    archiveName("dapp-sample-" + archiveVersion.get() + "." + archiveExtension.get());
    enableDebug = true
}

deployJar {
    endpoints {
        local {
            uri = 'http://localhost:9080/api/v3/icon_dex'
            nid = 0x3
        }
    }
    parameters {
        arg('value', 'hello')
    }
    keystore = "$projectDir/src/test/resources/keystore.json"
    password = file("$projectDir/src/test/resources/keysecret").text
}

generateContractSpec {
}

task generateContractSpecDAppSample(type: GenerateContractSpecTask) {
    dependsOn jarDAppSample
    jarFile = jarDAppSample.archiveFile
    contractSpecFile = file("$buildDir/generated/contractSpecDAppSample.json")
    doLast {
        def specFile = "$buildDir/generated/contractSpecDAppSample.json"
        def compactFile = specFile.replace(".json", ".compact.json")
        file(compactFile).text = "jq -c . $specFile".execute().text.trim()
    }
}

test {
    useJUnitPlatform()
    options {
        testLogging.showStandardStreams = true

        systemProperty 'url', 'http://localhost:19080/api/v3/icon'
        systemProperty 'nid', '0x3'
        systemProperty 'keyStore', "$keystoreName"
        systemProperty 'keyPassword', "$keystorePass"
        systemProperty 'address', "cxfdc9126bcd0416e30b907d7408810d7ba312d695"
        systemProperty 'isUpdate', "true"
        dependsOn optimizedJar
        systemProperty 'scoreFilePath', project.tasks.optimizedJar.outputJarName
    }
}
